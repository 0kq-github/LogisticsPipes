buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

ant.importBuild 'download-api.xml'
ant.importBuild 'load-fingerprint.xml'

apply plugin: 'forge'

version = "0.8.0"
def buildby = 'local'

def ENV = System.getenv()
if (ENV.BUILD_NUMBER) {
	version += "." + ENV.BUILD_NUMBER
	buildby = 'RS485-Jenkins'
} else {
	version += ".local"
}

group= "rs485.logisticspipes" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "logisticspipes"

minecraft {
    version = "1.7.2-10.12.1.1079"
    runDir = "run/assets"
    replace '%VERSION%', project.version
    replace '%MCVERSION%', "1.7.2"
    replace '%DEBUG%', "false"
	replaceIn "LogisticsPipes.java"
}

jar {
    manifest {
        attributes(	'FMLCorePlugin': 'logisticspipes.asm.LogisticsPipesCoreLoader', 
					'FMLCorePluginContainsFMLMod': 'true',
					'Implementation-Vendor': 'RS485',
					'Implementation-Title': 'LogisticsPipes',
					'Implementation-Version': version,
					'Built-By': buildby )
    }
}

// configure  the source folders
sourceSets {
    main {
        java {
            srcDir 'common'
            // exclude 'some exclusion'
            // include 'some inclusion'
        }
        resources {
            srcDir 'resources'
            // exclude 'some exclusion'
            // include 'some inclusion'
        }
    }
    api {
        java {
            srcDir 'dummy'
            srcDir 'api'
            // exclude 'some exclusion'
            // include 'some inclusion'
        }
	}
}

processResources {
    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        
        // replace version and mcversion
		expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

dependencies {
    compile 'com.mod-buildcraft:buildcraft:6.0.16:dev'
    compile 'org.projectlombok:lombok:1.12.6'
}

task deobfJar(type: Jar) {
	from sourceSets.main.output
	classifier = 'deobf'
}

task sourceJar(type: Jar) {
	from sourceSets.main.allSource
	classifier = 'sources'
}

artifacts {
	archives deobfJar
	archives sourceJar
	archives jar
}

tasks.compileApiJava.dependsOn('extract')
tasks.compileJava.dependsOn('loadFingerprints')
tasks.assemble.dependsOn('sourceJar', 'deobfJar', 'signJars')
